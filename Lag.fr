
(* -------------------------  *)
(*         Lagrangian         *)
(* -------------------------  *)

(* ---------------------  *)
(*         SM Lag         *)
(* ---------------------  *)

LGauge := Block[{mu,nu,ii,aa}, 
  ExpandIndices[-1/4 FS[B,mu,nu] FS[B,mu,nu] - 1/4 FS[Wi,mu,nu,ii] FS[Wi,mu,nu,ii] - 1/4 FS[G,mu,nu,aa] FS[G,mu,nu,aa], FlavorExpand->SU2W]];

LGhost := Block[{LGh1,LGhw,LGhs,LGhphi,mu, generators,gh,ghbar,Vectorize,phi1,phi2,togoldstones,doublet,doublet0},
  (* Pure gauge piece *) 	
  LGh1 = -ghBbar.del[DC[ghB,mu],mu];
  LGhw = -ghWibar.del[DC[ghWi,mu],mu];
  LGhs = -ghGbar.del[DC[ghG,mu],mu];

  gh    = {ghB, ghWi[1], ghWi[2], ghWi[3]};
  ghbar = {ghBbar, ghWibar[1], ghWibar[2], ghWibar[3]};
  generators = {-I/2 g1 IdentityMatrix[2], -I/2 gw PauliSigma[1], -I/2 gw PauliSigma[2], -I/2 gw PauliSigma[3]};
  doublet = Expand[{(-I phi1 - phi2)/Sqrt[2], Phi[2]} /. MR$Definitions /. vev -> 0]; 
  doublet0 = {0, vev/Sqrt[2]};
  Vectorize[{a_, b_}]:= Simplify[{Sqrt[2] Re[Expand[a]], Sqrt[2] Im[Expand[a]], Sqrt[2] Re[Expand[b]], Sqrt[2] Im[Expand[b]]}/.{Im[_]->0, Re[num_]->num}];
  togoldstones := {phi1 -> (GP + GPbar)/Sqrt[2], phi2 -> (-GP + GPbar)/(I Sqrt[2])};
  LGhphi=Plus@@Flatten[Table[-ghbar[[kkk]].gh[[lll]] Vectorize[generators[[kkk]].doublet0].Vectorize[generators[[lll]].(doublet+doublet0)],{kkk,4},{lll,4}]] /.togoldstones;

ExpandIndices[ LGhs + If[FeynmanGauge, LGh1 + LGhw + LGhphi,0], FlavorExpand->SU2W]];

LFermions := Block[{mu}, 
  ExpandIndices[I*(
    QLbar.Ga[mu].DC[QL, mu] + LLbar.Ga[mu].DC[LL, mu] + uRbar.Ga[mu].DC[uR, mu] + dRbar.Ga[mu].DC[dR, mu] + lRbar.Ga[mu].DC[lR, mu]), 
  FlavorExpand->{SU2W,SU2D}]/.{CKM[a_,b_] Conjugate[CKM[a_,c_]]->IndexDelta[b,c], CKM[b_,a_] Conjugate[CKM[c_,a_]]->IndexDelta[b,c]}];

LHiggs := Block[{ii,mu, feynmangaugerules},
  feynmangaugerules = If[Not[FeynmanGauge], {G0|GP|GPbar ->0}, {}];
 
  ExpandIndices[DC[Phibar[ii],mu] DC[Phi[ii],mu] + muH^2 Phibar[ii] Phi[ii] - lam Phibar[ii] Phi[ii] Phibar[jj] Phi[jj], FlavorExpand->{SU2D,SU2W}]/.feynmangaugerules
 ];

LYukawa := Block[{sp,ii,jj,cc,ff1,ff2,ff3,yuk,feynmangaugerules},
  feynmangaugerules = If[Not[FeynmanGauge], {G0|GP|GPbar ->0}, {}];
 
  yuk = ExpandIndices[
   -yd[ff2, ff3] CKM[ff1, ff2] QLbar[sp, ii, ff1, cc].dR [sp, ff3, cc] Phi[ii] - 
    yl[ff1, ff3] LLbar[sp, ii, ff1].lR [sp, ff3] Phi[ii] - 
    yu[ff1, ff2] QLbar[sp, ii, ff1, cc].uR [sp, ff2, cc] Phibar[jj] Eps[ii, jj], FlavorExpand -> SU2D];
  yuk = yuk /. { CKM[a_, b_] Conjugate[CKM[a_, c_]] -> IndexDelta[b, c], CKM[b_, a_] Conjugate[CKM[c_, a_]] -> IndexDelta[b, c]};
  yuk+HC[yuk]/.feynmangaugerules
 ];

(* -------------------------------  *)
(*         Pseudoscalar Lag         *)
(* -------------------------------  *)

(* --- Kinetic part --- *)

LEtakin := Block[{mu},
  ExpandIndices[
  1/2 DC[Eta,mu] DC[Eta,mu] -1/2 MEta^2 Eta Eta]];

(* --- Fermion coupling part --- *)

LEtaF := Block[{sp,cc,ff1,ff2,ff3,yuks,feynmangaugerules},
  feynmangaugerules = If[Not[FeynmanGauge], {G0|GP|GPbar ->0}, {}];

  yuks = ExpandIndices[
   - (I Cf)/fa yd[ff2, ff3] CKM[ff1, ff2] Ga[5] QLbar[sp, 2, ff1, cc].dR [sp, ff3, cc] Eta   
   - (I Cf)/fa yl[ff1, ff3] Ga[5] LLbar[sp, 2, ff1].lR [sp, ff3] Eta       
   - (I Cf)/fa yu[ff1, ff2] Ga[5] QLbar[sp, 1, ff1, cc].uR [sp, ff2, cc] Eta, FlavorExpand -> SU2D];
  yuks = yuks /. { CKM[a_, b_] Conjugate[CKM[a_, c_]] -> IndexDelta[b, c], CKM[b_, a_] Conjugate[CKM[c_, a_]] -> IndexDelta[b, c]};
  yuks+HC[yuks]/.feynmangaugerules
 ];

(* --- Gauge coupling part --- *)

LEtaG := Block[{mu,nu,ii,aa},
  ExpandIndices[CBa FS[B,mu,nu] Dual[FS][B,mu,nu] Eta + CWa FS[Wi,mu,nu,ii] Dual[FS][Wi,mu,nu,ii] Eta + CGa FS[G,mu,nu,aa] Dual[FS][G,mu,nu,aa] Eta, FlavorExpand->SU2W]];

(* --- Loop level couplings --- *)

LhaaEta := Block[{mu},
  ExpandIndices[Chaa H DC[Eta,mu] DC[Eta,mu] ]];

LhZaEta := Block[{mu},
   ExpandIndices[ChZa H DC[Eta,mu] Z[mu] ]];

LCHEta:= LGauge + LFermions + LHiggs + LYukawa + LGhost + LEtaF + LEtakin + LhaaEta  + LEtaG + LhZaEta;  
